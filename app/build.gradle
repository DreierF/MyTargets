/*
 * Copyright (C) 2017 Florian Dreier
 *
 * This file is part of MyTargets.
 *
 * MyTargets is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2
 * as published by the Free Software Foundation.
 *
 * MyTargets is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.triplet.play'
apply plugin: 'jacoco-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.google.firebase.firebase-crash'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion compileSdkVersionCode

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('../googlePlayAndroidDeveloper.json')
        }
    }

    defaultConfig {
        applicationId 'de.dreier.mytargets'
        minSdkVersion 16
        targetSdkVersion 27
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary true
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    dataBinding {
        enabled true
    }

    androidExtensions {
        experimental = true
    }

    signingConfigs {
        debug {
        }

        release {
            storeFile file(KEYSTORE_NAME)
            storePassword KEYSTORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            testCoverageEnabled true
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../tools/rules-proguard.pro'
        }
    }

    flavorDimensions "mode"

    productFlavors {
        dev {
            dimension "mode"
            multiDexEnabled true
        }
        regular {
            dimension "mode"
        }
        screengrab {
            dimension "mode"
        }
    }
}

project.afterEvaluate {
    android.applicationVariants.all { variant ->
        def realVariantName = variant.name
        def variantName = variant.name.capitalize()

        def task = project.tasks["jacocoTest${variantName}UnitTestReport"]
        def defaultExcludes = task.classDirectories.excludes

        def excludes = defaultExcludes

        def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/${realVariantName}", excludes: excludes)

        task.classDirectories.excludes = excludes
        task.classDirectories += kotlinDebugTree
    }
}

apply from: '../tools/script-codacy.gradle'
apply from: '../tools/script-play-store.gradle'

dependencies {
    kapt "com.android.databinding:compiler:$androidPluginVersion"
    implementation "com.android.support:support-compat:$libraryVersion.support"
    implementation "com.android.support:support-core-utils:$libraryVersion.support"
    implementation "com.android.support:support-core-ui:$libraryVersion.support"
    implementation "com.android.support:support-fragment:$libraryVersion.support"
    implementation "com.android.support:support-v13:$libraryVersion.support"
    implementation "com.android.support:appcompat-v7:$libraryVersion.support"
    implementation "com.android.support:design:$libraryVersion.support"
    implementation "com.android.support:support-annotations:$libraryVersion.support"
    implementation "com.android.support:preference-v14:$libraryVersion.support"
    implementation "com.android.support:recyclerview-v7:$libraryVersion.support"
    implementation "com.android.support:cardview-v7:$libraryVersion.support"
    implementation "com.android.support:preference-v14:$libraryVersion.support"

    implementation "com.google.android.gms:play-services-drive:$libraryVersion.playService"
    implementation "com.google.firebase:firebase-core:$libraryVersion.playService"
    implementation "com.google.firebase:firebase-crash:$libraryVersion.playService"

    implementation "com.google.code.gson:gson:$libraryVersion.gson"

    implementation "com.squareup.okhttp3:okhttp:$libraryVersion.okhttp"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$libraryVersion.okhttp"

    implementation "com.squareup.retrofit2:retrofit:$libraryVersion.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$libraryVersion.retrofit"

    implementation "com.squareup.picasso:picasso:$libraryVersion.picasso"

    implementation "com.evernote:android-state:$libraryVersion.androidState"
    kapt "com.evernote:android-state-processor:$libraryVersion.androidState"

    implementation "com.github.hotchemi:permissionsdispatcher:$libraryVersion.permissionsdispatcher"
    kapt "com.github.hotchemi:permissionsdispatcher-processor:$libraryVersion.permissionsdispatcher"

    implementation "com.anjlab.android.iab.v3:library:$libraryVersion.iab"

    implementation "com.github.PhilJay:MPAndroidChart:$libraryVersion.mpAndroidChart"

    implementation "org.adw.library:discrete-seekbar:$libraryVersion.discreteSeekbar"

    implementation "com.github.jkwiecien:EasyImage:$libraryVersion.easyImage"

    implementation "com.afollestad.material-dialogs:core:$libraryVersion.materialDialogs"

    implementation "com.github.medyo:android-about-page:$libraryVersion.aboutPage"

    implementation "com.github.oriley-me.homage:homage-core:$libraryVersion.homage"
    implementation "com.github.oriley-me.homage:homage-recyclerview:$libraryVersion.homage"

    implementation "com.makeramen:roundedimageview:$libraryVersion.roundedImageView"

    implementation "agency.tango.android:material-intro-screen:$libraryVersion.materialIntroScreen"

    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:$libraryVersion.dbFlow"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:$libraryVersion.dbFlow"
    implementation "com.github.Raizlabs.DBFlow:dbflow:$libraryVersion.dbFlow"

    implementation "com.github.chrisbanes:PhotoView:$libraryVersion.photoView"

    implementation "com.github.delight-im:Android-Languages:$libraryVersion.androidLanguages"

    implementation "uk.co.hassie:version-info-material-dialog:$libraryVersion.versionInfoDialog"

    implementation "com.github.gcacace:signature-pad:$libraryVersion.signaturePad"

    implementation project(':shared')
    wearApp project(':wearable')

    androidTestImplementation "com.google.truth:truth:$libraryVersion.truth"
    androidTestImplementation "com.google.code.findbugs:jsr305:$libraryVersion.findbugs"
    androidTestImplementation "com.android.support:support-annotations:$libraryVersion.support"
    androidTestImplementation "com.android.support.test:runner:$libraryVersion.testSupport"
    androidTestImplementation "com.android.support.test:rules:$libraryVersion.testSupport"
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$libraryVersion.uiAutomator"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$libraryVersion.espresso", {
        exclude group: 'com.android.support'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$libraryVersion.espresso", {
        exclude group: 'com.android.support'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-intents:$libraryVersion.espresso", {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    })

    implementation "com.android.support.test.espresso:espresso-idling-resource:$libraryVersion.espresso"
    androidTestScreengrabImplementation "tools.fastlane:screengrab:$libraryVersion.screengrab"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"

    implementation "com.android.support:multidex:$libraryVersion.multidex"
}

// Must be at the bottom
if (HAS_GMS_SECRET) {
    apply plugin: 'com.google.gms.google-services'
}
