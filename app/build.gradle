apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.triplet.play'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId 'de.dreier.mytargets'
        minSdkVersion 14
        targetSdkVersion 23
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }

    if (new File("gradle.properties").exists()) {
        signingConfigs {
            release {
                try {
                    storeFile file(KEYSTORE)
                    storePassword KEYSTORE_PASSWORD
                    keyAlias KEY_ALIAS
                    keyPassword KEY_PASSWORD
                } catch (ignored) {
                    throw new InvalidUserDataException('You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.')
                }
            }
        }

        buildTypes {
            debug {
                signingConfig signingConfigs.release
                minifyEnabled false
            }
            release {
                signingConfig signingConfigs.release
                shrinkResources true
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), '../proguard-rules.pro'
            }
        }
    }
    productFlavors {
        regular {}
        screengrab {}
    }
}

if (new File("gradle.properties").exists()) {
    afterEvaluate {
        task renameFolders {
            file('app/src/main/play/ca-ES').renameTo(file('app/src/main/play/ca'))
            file('app/src/main/play/sk-SK').renameTo(file('app/src/main/play/sk'))
            file('app/src/main/play/sl-SI').renameTo(file('app/src/main/play/sl'))
        }

        def generateReleasePlayResources = project.tasks.getByName('generateRegularReleasePlayResources')
        generateReleasePlayResources.dependsOn(':crowdinDownload')
        generateReleasePlayResources.dependsOn renameFolders
        generateReleasePlayResources.doLast {
            file('app/src/main/play/ca').renameTo(file('app/src/main/play/ca-ES'))
            file('app/src/main/play/sk').renameTo(file('app/src/main/play/sk-SK'))
            file('app/src/main/play/sl').renameTo(file('app/src/main/play/sl-SI'))
        }

        task generateLocalizedScreenshots(type: Exec, group: 'play store') {
            workingDir '..'
            commandLine './make_screenshots.sh'
        }
        generateLocalizedScreenshots.dependsOn(':crowdinDownload')
        generateLocalizedScreenshots.dependsOn assembleScreengrabDebugAndroidTest
        generateLocalizedScreenshots.dependsOn assembleScreengrabDebug
    }
    play {
        jsonFile = file(PLAY_STORE_JSON)
        uploadImages = true
    }
}

dependencies {
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:support-annotations:$supportLibVersion"
    compile "com.android.support:preference-v7:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"

    compile "com.google.android.gms:play-services-wearable:$playServiceVersion"

    compile 'com.google.code.gson:gson:2.6.2'

    compile 'joda-time:joda-time:2.9.4'

    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.2.0'

    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'

    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'

    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'

    compile 'com.github.hotchemi:permissionsdispatcher:2.1.2'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.1.2'

    compile "org.parceler:parceler-api:$parcelerVersion"
    apt "org.parceler:parceler:$parcelerVersion"

    compile 'com.anjlab.android.iab.v3:library:1.0.31'

    compile 'com.bignerdranch.android:recyclerview-multiselect:0.2'

    compile 'com.github.PhilJay:MPAndroidChart:v2.2.4'

    compile 'org.adw.library:discrete-seekbar:1.0.1'

    compile 'com.github.jkwiecien:EasyImage:1.2.1'

    compile('com.github.afollestad.material-dialogs:core:0.8.5.8@aar') {
        transitive = true
    }

    compile 'com.github.medyo:android-about-page:1.0.6'

    compile 'me.oriley.homage:homage-core:0.2.0'
    compile 'me.oriley.homage:homage-recyclerview:0.2.0'

    compile project(':shared')
    wearApp project(':wearable')

    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.0'
    androidTestCompile "com.android.support:support-v4:$supportLibVersion"
    androidTestCompile "com.android.support:appcompat-v7:$supportLibVersion"
    androidTestCompile "com.android.support:design:$supportLibVersion"
    androidTestCompile "com.android.support:support-annotations:$supportLibVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$supportLibVersion"
    androidTestCompile "com.android.support.test:runner:$testSupportLibVersion"
    androidTestCompile "com.android.support.test:rules:$testSupportLibVersion"
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestScreengrabCompile 'tools.fastlane:screengrab:0.3.2'
}