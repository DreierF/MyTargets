/*
 * Copyright (C) 2016 Florian Dreier
 *
 * This file is part of MyTargets.
 *
 * MyTargets is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2
 * as published by the Free Software Foundation.
 *
 * MyTargets is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

import com.android.build.gradle.internal.tasks.databinding.DataBindingExportBuildInfoTask

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.triplet.play'
apply plugin: 'jacoco-android'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion compileSdkVersionCode
    buildToolsVersion buildToolsVersionCode

    defaultConfig {
        applicationId 'de.dreier.mytargets'
        minSdkVersion 14
        targetSdkVersion 25
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        debug {
        }
        release {
            try {
                storeFile file('../keystore.jks')
                storePassword project.hasProperty('KEYSTORE_PASSWORD') ? KEYSTORE_PASSWORD : System.getenv('KEYSTORE_PASSWORD')
                keyAlias 'mytargets'
                keyPassword project.hasProperty('KEY_PASSWORD') ? KEY_PASSWORD : System.getenv('KEY_PASSWORD')
            } catch (ignored) {
                throw new InvalidUserDataException('You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.')
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            minifyEnabled false
            testCoverageEnabled true
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }


    productFlavors {
        regular {}
        screengrab {}
    }
}

/*
 * Workaround for https://code.google.com/p/android/issues/detail?id=182715
 *
 * The Android Gradle plugin is creating DataBindingExportBuildInfoTasks for the instrumentation
 * APK that generates from the app APKs layouts. This creates duplicate classes in the app and
 * instrumentation APK which leads to
 *      java.lang.IllegalAccessError: Class ref in pre-verified class resolved to unexpected implementation
 * on older devices.
 *
 * The workaround is to get the DataBindingExportBuildInfoTasks tasks for the instrumentation APK
 * and delete the files right after it creates them.
 */
tasks.withType(DataBindingExportBuildInfoTask) { task ->
    if (task.name.endsWith('AndroidTest')) {
        task.finalizedBy(tasks.create("${task.name}Workaround") << {
            task.output.deleteDir()
        })
    }
}

task renameFolders {
    file('app/src/main/play/ca-ES').renameTo(file('app/src/main/play/ca'))
    file('app/src/main/play/sk-SK').renameTo(file('app/src/main/play/sk'))
    file('app/src/main/play/sl-SI').renameTo(file('app/src/main/play/sl'))
}

def CROWDIN_API_KEY = project.hasProperty('CROWDIN_API_KEY') ? CROWDIN_API_KEY : System.getenv('CROWDIN_API_KEY')
if (CROWDIN_API_KEY != null && !CROWDIN_API_KEY.isEmpty()) {
    tasks.whenTaskAdded { task ->
        if (task.name.equals("assembleRegularRelease")) {
            task.dependsOn(':crowdinDownload')
        } else if (task.name.equals("generateRegularReleasePlayResources")) {
            task.dependsOn(':crowdinDownload')
            task.dependsOn renameFolders
            task.doLast {
                file('app/src/main/play/ca').renameTo(file('app/src/main/play/ca-ES'))
                file('app/src/main/play/sk').renameTo(file('app/src/main/play/sk-SK'))
                file('app/src/main/play/sl').renameTo(file('app/src/main/play/sl-SI'))
            }
        }
    }

    task generateLocalizedScreenshots(type: Exec, group: 'play store', dependsOn: [':crowdinDownload', 'assembleScreengrabDebugAndroidTest', 'assembleScreengrabDebug']) {
        workingDir '..'
        commandLine './make_screenshots.sh'
    }

    if (new File('googlePlayAndroidDeveloper.json').exists()) {
        play {
            jsonFile = file('../googlePlayAndroidDeveloper.json')
        }
    }
}

/*
* Workaround for https://code.google.com/p/android/issues/detail?id=71365
* */
def buildConfigAndResStringField(variant, name, value) {
    variant.resValue 'string', name.toLowerCase(), value
    variant.buildConfigField 'String', name, "\"$value\""
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.resValue 'string', 'application_id', variant.applicationId
        variant.resValue 'string', 'content_authority', variant.applicationId + ".provider"
        buildConfigAndResStringField variant, "ACCOUNT_TYPE", variant.applicationId
        buildConfigAndResStringField variant, "CONTENT_AUTHORITY", variant.applicationId + ".provider"
    }
}

dependencies {
    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:support-compat:$supportLibVersion"
    compile "com.android.support:support-core-utils:$supportLibVersion"
    compile "com.android.support:support-core-ui:$supportLibVersion"
    compile "com.android.support:support-fragment:$supportLibVersion"
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:support-annotations:$supportLibVersion"
    compile "com.android.support:preference-v7:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"

    compile "com.google.android.gms:play-services-wearable:$playServiceVersion"
    compile "com.google.android.gms:play-services-drive:$playServiceVersion"

    compile 'com.google.code.gson:gson:2.8.0'

    compile "joda-time:joda-time:$jodaTimeVersion"

    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.4.2'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'

    compile 'com.github.hotchemi:permissionsdispatcher:2.1.3'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.1.3'

    compile "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"

    compile 'com.anjlab.android.iab.v3:library:1.0.34'

    compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'

    compile 'com.github.clans:fab:1.6.4'

    compile 'org.adw.library:discrete-seekbar:1.0.1'

    compile 'com.github.jkwiecien:EasyImage:1.3.1'

    compile 'com.afollestad.material-dialogs:core:0.9.1.0'

    compile 'com.github.medyo:android-about-page:1.1.1'

    compile 'com.github.oriley-me.homage:homage-core:0.2.0'
    compile 'com.github.oriley-me.homage:homage-recyclerview:0.2.0'

    compile 'com.makeramen:roundedimageview:2.2.1'

    compile(name: 'dropbox-android-core-2-1-2', ext: 'aar')
    compile 'com.fasterxml.jackson.core:jackson-core:2.8.5'
    compile 'javax.servlet:servlet-api:2.5'

    compile project(':shared')
    wearApp project(':wearable')

    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
    androidTestCompile "com.android.support:support-v4:$supportLibVersion"
    androidTestCompile "com.android.support:support-compat:$supportLibVersion"
    androidTestCompile "com.android.support:support-core-utils:$supportLibVersion"
    androidTestCompile "com.android.support:support-core-ui:$supportLibVersion"
    androidTestCompile "com.android.support:support-fragment:$supportLibVersion"
    androidTestCompile "com.android.support:appcompat-v7:$supportLibVersion"
    androidTestCompile "com.android.support:design:$supportLibVersion"
    androidTestCompile "com.android.support:support-annotations:$supportLibVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$supportLibVersion"
    androidTestCompile "com.android.support.test:runner:$testSupportLibVersion"
    androidTestCompile "com.android.support.test:rules:$testSupportLibVersion"
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestScreengrabCompile 'tools.fastlane:screengrab:0.3.2'
}
