/*
 * Copyright (C) 2017 Florian Dreier
 *
 * This file is part of MyTargets.
 *
 * MyTargets is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2
 * as published by the Free Software Foundation.
 *
 * MyTargets is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'
apply plugin: 'jacoco-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.google.firebase.firebase-crash'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion compileSdkVersionCode

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('../googlePlayAndroidDeveloper.json')
        }
    }

    defaultConfig {
        applicationId 'de.dreier.mytargets'
        minSdkVersion 16
        targetSdkVersion 27
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary true
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    dataBinding {
        enabled true
    }

    androidExtensions {
        experimental true
    }

    signingConfigs {
        debug {
        }

        release {
            storeFile file(KEYSTORE_NAME)
            storePassword KEYSTORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../tools/rules-proguard-debug.pro'
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../tools/rules-proguard.pro'
        }
    }

    flavorDimensions "mode"

    productFlavors {
        dev {
            dimension "mode"
            multiDexEnabled true
        }
        regular {
            dimension "mode"
        }
        screengrab {
            dimension "mode"
        }
    }

    splits {

        // Configures multiple APKs based on ABI.
        abi {

            // Enables building multiple APKs per ABI.
            enable true

            // By default all ABIs are included, so use reset() and include to specify that we only
            // want APKs for x86, armeabi-v7a, and mips.

            // Resets the list of ABIs that Gradle should create APKs for to none.
            reset()

            // Specifies a list of ABIs that Gradle should create APKs for.
            include 'armeabi-v7a'//, 'arm', 'arm-v7a', 'arm64', 'mips', 'x86', 'x86_64'

            // Specifies that we do not want to also generate a universal APK that includes all ABIs.
            universalApk false
        }
    }
}

project.afterEvaluate {
    android.applicationVariants.all { variant ->
        def realVariantName = variant.name
        def variantName = variant.name.capitalize()

        def task = project.tasks["jacocoTest${variantName}UnitTestReport"]
        def defaultExcludes = task.classDirectories.excludes

        def excludes = defaultExcludes

        def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/${realVariantName}", excludes: excludes)

        task.classDirectories.excludes = excludes
        task.classDirectories += kotlinDebugTree
    }
}

apply from: '../tools/script-codacy.gradle'
apply from: '../tools/script-play-store.gradle'

dependencies {
    Map<String, String> dependencies = rootProject.ext.dependencies

    kapt dependencies.databindingCompiler

    implementation dependencies.kotlinStdlibJre7

    implementation dependencies.supportCompat
    implementation dependencies.supportCoreUtils
    implementation dependencies.supportCoreUi
    implementation dependencies.supportFragment
    implementation dependencies.supportV13
    implementation dependencies.appCompat
    implementation dependencies.supportDesign
    implementation dependencies.supportAnnotations
    implementation dependencies.supportPreference
    implementation dependencies.recyclerView
    implementation dependencies.cardView

    implementation dependencies.playServicesDrive
    implementation dependencies.firebaseCore
    implementation dependencies.firebaseCrash

    implementation dependencies.gson

    implementation dependencies.okHttp
    implementation dependencies.okHttpUrlConnection

    implementation dependencies.retrofit
    implementation dependencies.retrofitConverterGson

    implementation dependencies.picasso

    implementation dependencies.androidState
    kapt dependencies.androidStateProcessor

    implementation dependencies.permissionsDispatcher
    kapt dependencies.permissionsDispatcherProcessor

    implementation dependencies.iab

    implementation dependencies.mpAndroidChart

    implementation dependencies.discreteSeekbar

    implementation dependencies.easyImage

    implementation dependencies.materialDialogs

    implementation dependencies.androidAboutPage

    implementation dependencies.homageCore
    implementation dependencies.homageRecyclerView

    implementation dependencies.roundedImageView

    implementation dependencies.materialIntroScreen
    implementation dependencies.openCV

    kapt dependencies.dbFlowProcessor
    implementation dependencies.dbFlowCore
    implementation dependencies.dbFlow

    implementation dependencies.photoView

    implementation dependencies.androidLanguages

    implementation dependencies.versionInfoMaterialDialog

    implementation dependencies.signaturePad

    implementation project(':shared')
    wearApp project(':wearable')

    testImplementation dependencies.jUnit
    testImplementation dependencies.kotlinTest
    testImplementation dependencies.kotlinTestJunit

    androidTestImplementation dependencies.truth
    androidTestImplementation dependencies.jsr305
    androidTestImplementation dependencies.runner
    androidTestImplementation dependencies.rules
    androidTestImplementation dependencies.uiAutomator
    androidTestImplementation(dependencies.espressoCore, {
        exclude group: 'com.android.support'
    })
    androidTestImplementation(dependencies.espressoContrib, {
        exclude group: 'com.android.support'
    })
    androidTestImplementation(dependencies.espressoIntents, {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    })

    implementation dependencies.espressoIdlingResource
    androidTestScreengrabImplementation dependencies.screengrab

    implementation dependencies.multiDex
}

// Must be at the bottom
if (HAS_GMS_SECRET) {
    apply plugin: 'com.google.gms.google-services'
}
