apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion compileSdkVersionCode
    buildToolsVersion buildToolsVersionCode

    defaultConfig {
        applicationId 'de.dreier.mytargets'
        minSdkVersion 20
        targetSdkVersion 25
        versionCode appVersionCode
        versionName appVersionName
    }

    signingConfigs {
        debug {
        }
        release {
            try {
                storeFile file('../keystore.jks')
                storePassword project.hasProperty('KEYSTORE_PASSWORD') ? KEYSTORE_PASSWORD : System.getenv('KEYSTORE_PASSWORD')
                keyAlias 'mytargets'
                keyPassword project.hasProperty('KEY_PASSWORD') ? KEY_PASSWORD : System.getenv('KEY_PASSWORD')
            } catch (ignored) {
                throw new InvalidUserDataException('You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.')
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.release
            minifyEnabled false
            testCoverageEnabled = true
        }
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), '../proguard-rules.pro'
        }
    }
}

afterEvaluate {
    assembleRelease.dependsOn(':crowdinDownload')
}

dependencies {
    compile 'com.google.android.support:wearable:1.4.0'
    compile "com.google.android.gms:play-services-wearable:$playServiceVersion"

    compile "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"

    compile project(':shared')
}