/*
 * Copyright (C) 2017 Florian Dreier
 *
 * This file is part of MyTargets.
 *
 * MyTargets is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2
 * as published by the Free Software Foundation.
 *
 * MyTargets is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        //classpath 'com.github.triplet.gradle:play-publisher:1.1.5' //for now until triplet gets the mapping upload
        classpath 'com.github.bobvanderlinden:gradle-play-publisher:1.2.0-beta2'
        classpath 'org.ajoberstar:grgit:1.8.0'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.google.firebase:firebase-plugins:1.0.4'
    }

    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

plugins {
    id 'com.mendhak.gradlecrowdin' version '0.0.9'
    id 'com.github.ben-manes.versions' version '0.13.0'
}

ext {
    compileSdkVersionCode = 25
    buildToolsVersionCode = '25.0.2'

    git = Grgit.open()
    appVersionCode = Integer.parseInt("${git.tag.list().size()}")
    appVersionName = "${git.describe()}"
            .replace("v", "")
            .replace("_b", "-Beta")
            .replace("_rc", "-RC")

    libraryVersion = [
        support : '25.1.0',
        testSupport : '0.5',
        supportWearable : '1.4.0',
        playService : '10.0.1',
        firebase: '10.0.1',
        espresso : '2.2.2', // On update also pull the latest changes for our intents copy
        parceler : '1.1.6',
        jodaTime : '2.9.7',
        dbFlow : '3.1.1',
        gson : '2.8.0',
        okhttp : '3.5.0',
        retrofit : '2.1.0',
        picasso : '2.5.2',
        icepick : '3.2.0',
        permissionsdispatcher : '2.3.1',
        iab : '1.0.38',
        mpAndroidChart : 'v3.0.1',
        clansFab : '1.6.4',
        discreteSeekbar : '1.0.1',
        easyImage : '1.4.2',
        materialDialogs : '0.9.2.2',
        aboutPage : '1.1.1',
        homage : '0.2.0',
        roundedImageView : '2.2.1',
        truth : '0.30',
        robolectric : '3.1.4',
        findbugs : '3.0.1',
        uiAutomator : '2.1.2',
        screengrab : '0.5.6',
        annimonStream : '1.1.4',
        jUnit : '4.12',
        mockito : '2.5.5',
        materialIntroScreen: '0.0.5'
    ]
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://clojars.org/repo/' }
    }
}

apply from: "crowdin.gradle"

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}