/*
 * Copyright (C) 2017 Florian Dreier
 *
 * This file is part of MyTargets.
 *
 * MyTargets is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2
 * as published by the Free Software Foundation.
 *
 * MyTargets is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

import org.ajoberstar.grgit.Grgit

buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'me.tatarka:gradle-retrolambda:3.6.0'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.0-beta2'
        classpath 'org.ajoberstar:grgit:1.8.0'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.3'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.google.firebase:firebase-plugins:1.0.5'
    }

    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

plugins {
    id 'com.mendhak.gradlecrowdin' version '0.0.9'
    id 'com.github.ben-manes.versions' version '0.14.0'
}

ext {
    compileSdkVersionCode = 25
    buildToolsVersionCode = '25.0.2'

    git = Grgit.open()
    appVersionCode = Integer.parseInt("${git.tag.list().size()}")
    appVersionName = "${git.describe()}"
            .replace("v", "")
            .replace("_b", "-Beta")
            .replace("_rc", "-RC")

    libraryVersion = [
            aboutPage            : '1.2',
            annimonStream        : '1.1.6',
            clansFab             : '1.6.4',
            dbFlow               : '3.1.1',
            dexmaker             : '2.2.0',
            discreteSeekbar      : '1.0.1',
            easyImage            : '1.4.2',
            espresso             : '2.2.2',
            findbugs             : '3.0.1',
            firebase             : '10.2.1',
            gson                 : '2.8.0',
            homage               : '0.2.0',
            iab                  : '1.0.38',
            icepick              : '3.2.0',
            jodaTime             : '2.9.7',
            jUnit                : '4.12',
            materialDialogs      : '0.9.4.1',
            materialIntroScreen  : '0.0.5',
            mpAndroidChart       : 'v3.0.1',
            mockito              : '2.7.17',
            okhttp               : '3.6.0',
            parceler             : '1.1.6',
            permissionsdispatcher: '2.3.2',
            picasso              : '2.5.2',
            playService          : '10.2.1',
            screengrab           : '1.0.2',
            retrofit             : '2.2.0',
            roundedImageView     : '2.2.1',
            support              : '25.3.0',
            supportWearable      : '2.0.0',
            testSupport          : '0.5',
            truth                : '0.32',
            uiAutomator          : '2.1.2'
    ]
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://clojars.org/repo/' }
    }
}

apply from: "crowdin.gradle"

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['alpha', 'preview', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}